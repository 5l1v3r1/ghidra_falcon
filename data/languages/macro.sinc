macro store32(addr, value) {
  *[dram]:4 (addr) = value;
}
macro store8(addr, value) {
  *[dram]:1 (addr) = value;
}
macro load32(dst1, addr) {
  dst1 = *[dram]:4 (addr);
}
macro load8(dst1, addr) {
  dst1 = *[dram]:1 (addr);
}

macro xbit(dst1, src1, bit1) {
  dst1 = (src1 >> bit1) & 1;
}
macro bclr(reg1, bit1) {
  reg1 = reg1 & ~(1 << bit1);
}

macro bset(reg1, bit1) {
  reg1 = reg1 | (1 << bit1);
}

macro push32(val32) {
  sp = sp - 4;
  *[dram]:4 sp = val32;
}

macro pop32(ret32) {
  sp = sp + 4;
  ret32 = *[dram]:4 sp;
}

macro ret() {
  pop32(pc);
  return [pc];
}

macro setResultFlags(result) {
  $(Z_flag) = (result == 0);
  $(S_flag) = (result s< 0);
}

macro and(dst, src1, src2) {
  dst = src1 & src2;

  $(C_flag) = 0;
  $(O_flag) = 0;
  setResultFlags(dst);
}

macro or(dst, src1, src2) {
  dst = src1 | src2;

  $(C_flag) = 0;
  $(O_flag) = 0;
  setResultFlags(dst);
}

macro xor(dst, src1, src2) {
  dst = src1 ^ src2;

  $(C_flag) = 0;
  $(O_flag) = 0;
  setResultFlags(dst);
}

macro shl32(dst, src1, src2) {
  dst = src1 << (src2 & 0x1F);

  $(O_flag) = 0;
  setResultFlags(dst);
}

macro shr32(dst, src1, src2) {
  dst = src1 >> (src2 & 0x1F);

  $(O_flag) = 0;
  setResultFlags(dst);
}

macro add(dst, src1, src2) {
  $(C_flag) = carry(src1,src2);
  $(O_flag) = scarry(src1,src2);
  dst = src1 + src2;
  setResultFlags(dst);
}

macro sub(dst, src1, src2) {
  $(C_flag) = src1 < src2;
  $(O_flag) = sborrow(src1,src2);
  dst = src1 - src2;
  setResultFlags(dst);
}

macro cmp(src1, src2) {
  # TODO: check flag correctness
  setResultFlags((src1 - src2));
  $(C_flag) = src1 < src2;
  $(O_flag) = sborrow(src1, src2);
}

macro cmpu(src1, src2) {
  # TODO: is this right?
  $(C_flag) = (src1 < src2);
  $(Z_flag) = (src1 == src2);
}

macro cmps(src1, src2) {
  $(C_flag) = (src1 s< src2);
  $(Z_flag) = (src1 == src2);
}


# TODO: check if that set flags on hardware
macro mulu(dst, src1, src2) {
  dst = zext(src1) * zext(src2);
}